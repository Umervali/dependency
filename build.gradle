buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1'
		classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:latest.release'
	}
}

plugins {
	id 'java-gradle-plugin'
	id 'com.gradle.plugin-publish' version '0.9.10'
	id 'nebula.plugin-plugin' version '6.3.0'
    id 'nebula.kotlin' version '1.2.41'
    id 'groovy'
}

apply plugin: 'com.github.hierynomus.license'

group = 'io.spring.gradle'

description 'Inline dependency locking for Gradle'

contacts {
    'jschneider@pivotal.io' {
        moniker 'Jon Schneider'
        github 'jkschneider'
    }
}

dependencies {
    compileOnly 'org.codehaus.groovy:groovy-all:2.4.12'
	compile 'org.jetbrains.kotlin:kotlin-reflect'
}

pluginBundle {
    plugins {
        kotlin {
            id = 'spring.lock'
            displayName = 'Spring Dependency Lock'
            description = project.description
            tags = ['spring', 'dependency', 'lock']
        }
    }

    website = 'https://github.com/spring-gradle-plugins/dependency-lock'
    vcsUrl = 'https://github.com/spring-gradle-plugins/dependency-lock.git'
    description = 'Plugin to lock dynamic dependencies and forces'

	mavenCoordinates {
		groupId = 'io.spring.gradle'
		artifactId = 'dependency-lock'
	}
}

bintray.pkg {
	repo = 'jars'
	userOrg = 'spring'
	websiteUrl = 'https://github.com/spring-gradle-plugins/dependency-lock'
	vcsUrl = 'https://github.com/spring-gradle-plugins/dependency-lock.git'
	issueTrackerUrl = 'https://github.com/spring-gradle-plugins/dependency-lock/issues'
	labels = ['gradle', 'spring', 'dependency', 'lock']
}

license {
	header = file('gradle/licenseHeader.txt')
	exclude '**/*.tokens'
	mapping {
		kt = 'JAVADOC_STYLE'
	}
	sourceSets = project.sourceSets
	strictCheck = true
}

bintray.pkg {
	version {
		gpg {
			sign = false
		}
	}
}

compileGroovy.dependsOn.remove('compileJava')
compileKotlin.dependsOn compileGroovy
compileKotlin.classpath += files(compileGroovy.destinationDir)
classes.dependsOn compileKotlin
